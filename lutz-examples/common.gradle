// overrides default "build"
buildDir = file("target")

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext.getNightly = { flixHome ->
    def jars = fileTree(flixHome).filter { it.name.matches("flix-.*\\.jar") }.files
    if (jars.size() > 0) {
        def jarName = jars.last().name
        logger.warn("Nightly build '$jarName' was selected")
        return jarName
    }
    else {
        logger.warn("> WARNING: No nightly build of Flix was found; use release version instead.")
        return "flix.jar"
    }
}

ext.convertPath = { path ->
    if (path.startsWith("/cygdrive/")) {
        return path[10] + ":" + path.substring(11)
    }
    else {
        return path
    }
}

project.ext {

    configurations {
       bootClasspath
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // see flix.jar:library.properties 
        bootClasspath "org.scala-lang:scala-library:2.13.17"
    }

    flixHome = System.getenv("FLIX_HOME")
    logger.info("flixHome = $flixHome")

    jarName = System.getProperty("nightly") != null
        ? getNightly(flixHome)
        : "flix.jar"
    flixJar = file(convertPath(flixHome) + "/" + jarName)

    mainSourceTree = fileTree(dir: "src/main", include: "**/*.flix")
    //testSourceTree = fileTree(dir: "src/test", include: "**/*.flix")

    buildAppDir = file("${buildDir}/${appName}")
}

clean.doLast {
    buildDir.deleteDir()
}

tasks.withType(JavaCompile) {
    // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.CompileOptions.html
    options.deprecation true
    options.encoding "UTF8"
}

task initFlix(type: JavaExec) {
    jvmArgs "-Dfile.encoding=UTF-8"

    mainClass = "-jar"
    args = [ flixJar, "init" ]

    workingDir buildAppDir
}

initFlix.doFirst {
    buildAppDir.mkdirs()
}

initFlix.onlyIf {
    ! buildAppDir.exists()
}

task deleteFiles(type: Delete) {
    dependsOn initFlix

    delete fileTree("${buildAppDir}/src") { include "**/*.flix" }
    delete fileTree("${buildAppDir}/test") { include "**/*.flix" }
}

task copyFlix(type: Copy) {
    dependsOn initFlix, deleteFiles

    from("src/main") { include "**/*.flix" }
    into "${buildAppDir}/src"
}

task compileFlix(type: JavaExec) {
    dependsOn copyFlix, compileJava

    description = "Compile Flix source files"

    jvmArgs "-Dfile.encoding=UTF-8"

    mainClass = "-jar"
    args = [ flixJar, "build" ]

    workingDir = buildAppDir
}

compileFlix.onlyIf {
    ! mainSourceTree.isEmpty()
}

task jarFlix(type: JavaExec) {
    dependsOn compileFlix

    description = "Create Flix program \"${appName}/${appName}.jar\""

    jvmArgs "-Dfile.encoding=UTF-8"

    mainClass = "-jar"
    args = [ flixJar, "build-jar" ]

    workingDir = buildAppDir
}

build {
    dependsOn jarFlix
}

task run(type: JavaExec) {
    dependsOn build

    description = "Execute Flix program \"${appName}/${appName}.jar\""

    jvmArgs "-Dfile.encoding=UTF-8"

    def bootClasspath = fileTree(dir: "${buildAppDir}/lib", include: "**/*.jar").join(File.pathSeparator)
    // Starting with version 0.35.0 Flix generates the jar file into directory 'artifact'.
    def appJar = "${buildAppDir}${File.separator}artifact${File.separator}${appName}.jar"

    mainClass = "-Xbootclasspath/a:${bootClasspath}"
    args = [ "-jar", "${appJar}" ]

    workingDir = buildAppDir
}
